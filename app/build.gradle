plugins {
    id 'com.android.application'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.google.gms.google-services'
    id 'io.fabric'
}

static String getBranchName() {
    final branchCommand = ["git", "symbolic-ref", "HEAD", "--short"]
    return branchCommand.execute().text.trim()
}

/**
 * The depth of the commit, using the first parent in the graph for
 * merge commits, which means staying "in" the branch that is
 * merged into.
 */
static int gitFirstParentDepth() {
    final command = ["git", "rev-list", "HEAD", "--first-parent", "--count"]
    final stdout = command.execute().text.trim()
    return Integer.parseInt(stdout)
}

static boolean isVersioned() {
    return getBranchName() == "master"
}

/**
 * The android "version code".
 */
static int getVersionCode() {
    if (isVersioned()) {
        // Providing all releases are made from the same branch, and
        // don't try to release the same commit twice, this gives an
        // automatic, always-increasing version number. It avoids the need to
        // take care with test-server build job numbers (i.e. back them up
        // and be able to restore them), and avoids the need to update the
        // version number manually.
        return gitFirstParentDepth()
    } else {
        return 1
    }
}

/**
 * The android "version name".
 */
static String getVersionName() {
    if (!isVersioned()) {
        return "0.0.1"
    }
    return "0.0.${getVersionCode()}"
}

def releaseStoreFile = file(RELEASE_STORE_FILE, PathValidation.NONE)

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "uk.me.jeremygreen.merging"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile releaseStoreFile
            storePassword file(RELEASE_STORE_PASSWORD_FILE).getText('UTF-8').trim()
            keyPassword file(RELEASE_KEY_PASSWORD_FILE).getText('UTF-8').trim()
            keyAlias RELEASE_KEY_ALIAS
        }
    }
    buildTypes {
        release {
            if (releaseStoreFile.exists() && releaseStoreFile.size() > 0) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def lifecycle_version = "2.2.0"
    def room_version = "2.1.0"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5"
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${lifecycle_version}"
//    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${lifecycle_version}"
    implementation "androidx.room:room-ktx:${room_version}"
    implementation "androidx.room:room-runtime:${room_version}"
    implementation 'androidx.viewpager2:viewpager2:1.0.0-alpha05'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.facebook.fresco:fresco:1.13.0'
    implementation 'com.google.firebase:firebase-core:17.0.0'
    implementation 'com.google.firebase:firebase-ml-vision:20.0.0'
    implementation 'com.google.firebase:firebase-ml-vision-face-model:17.0.2'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    kapt "androidx.lifecycle:lifecycle-compiler:${lifecycle_version}"
    kapt "androidx.room:room-compiler:${room_version}"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "androidx.room:room-testing:${room_version}"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

task writeVersionFile {
    doLast {
        file("${buildDir}/version").write(getVersionName(), "UTF-8")
    }
}
assemble.dependsOn writeVersionFile
